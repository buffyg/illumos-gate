/*
 * CDDL HEADER START
 *
 * The contents of this file are subject to the terms of the
 * Common Development and Distribution License, Version 1.0 only
 * (the "License").  You may not use this file except in compliance
 * with the License.
 *
 * You can obtain a copy of the license at usr/src/OPENSOLARIS.LICENSE
 * or http://www.opensolaris.org/os/licensing.
 * See the License for the specific language governing permissions
 * and limitations under the License.
 *
 * When distributing Covered Code, include this CDDL HEADER in each
 * file and include the License file at usr/src/OPENSOLARIS.LICENSE.
 * If applicable, add the following below this CDDL HEADER, with the
 * fields enclosed by brackets "[]" replaced with your own identifying
 * information: Portions Copyright [yyyy] [name of copyright owner]
 *
 * CDDL HEADER END
 */
/*
 * Copyright 2004 Sun Microsystems, Inc.  All rights reserved.
 * Use is subject to license terms.
 */

#pragma ident	"%Z%%M%	%I%	%E% SMI"

#pragma dictionary "SUN4U"

#define	HB_FIT		1000
#define HBUS_FIT	1000
#define PCI_BUS_FIT	500
#define PCI_DEV_FIT	1000
#define	CPU_FIT		500

#define	PCI_HB_DEV_PATH hostbridge/pcibus/pcidev[32]/pcifn[0]

fru hostbridge;
asru hostbridge;

event fault.io.tomatillo@hostbridge,
    FITrate=HB_FIT, FRU=hostbridge, ASRU=hostbridge;

event error.io.tom.jbus.um@hostbridge;
event error.io.tom.jbus.to@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.jbus.bus@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.jbus.iis@hostbridge;
event error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.mmu.to@hostbridge;

event ereport.io.tom.jbus.um@hostbridge{within(5s)};
event ereport.io.tom.jbus.to@hostbridge{within(5s)};
event ereport.io.tom.jbus.bus@hostbridge{within(5s)};
event ereport.io.tom.jbus.to-exp@hostbridge{within(5s)};
event ereport.io.tom.jbus.iis@hostbridge{within(5s)};
event ereport.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.jbus.srds@hostbridge{within(5s)};
event ereport.io.tom.jbus.srdsa@hostbridge{within(5s)};
event ereport.io.tom.jbus.sown@hostbridge{within(5s)};
event ereport.io.tom.jbus.srdo@hostbridge{within(5s)};
event ereport.io.pci.rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.target-rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.sta@hostbridge/pcibus/pcidev/pcifn{within(5s)};

/*
 * A faulty Tomatillo hostbridge may cause:
 *
 *  - um:	jbus unmapped address error.
 *  - to:	jbus timeout.
 *  - bus:	jbus bus error.
 *  - iis:	illegal coherency install state error.
 *  - inval:	iommu invalid entry error.
 *  - prot:	iommu protection error, attempted to write a read-only page.
 *  - bva:	iommu bad virtual address, address out of range.
 *  - btt:	iommu bad tsb size tbw size combination.
 *  - srds:	snoop error due to own RDS hitting cache line in S, O or M.
 *  - srdsa:	snoop error due to own RDSA hitting cache line in S, O or M.
 *  - sown:	snoop error due to own OWN hitting cache line in S or M.
 *  - srdo:	snoop error due to own RDO hitting cache line in O or M.
 */
prop fault.io.tomatillo@hostbridge (0)->
    error.io.tom.jbus.um@hostbridge,
    error.io.tom.jbus.to@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.jbus.bus@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.jbus.iis@hostbridge,
    error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn,
    ereport.io.tom.jbus.srds@hostbridge,
    ereport.io.tom.jbus.srdsa@hostbridge,
    ereport.io.tom.jbus.sown@hostbridge,
    ereport.io.tom.jbus.srdo@hostbridge;

prop error.io.tom.jbus.um@hostbridge (1)->
    ereport.io.tom.jbus.um@hostbridge;

prop error.io.tom.jbus.to@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.jbus.to@hostbridge,
    ereport.io.tom.jbus.to-exp@hostbridge;

prop error.io.tom.jbus.to@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.mmu.to@hostbridge,
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.jbus.bus@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.jbus.bus@hostbridge;

prop error.io.tom.jbus.bus@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.jbus.iis@hostbridge (1)->
    ereport.io.tom.jbus.iis@hostbridge;

prop error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.mmu.btt@PCI_HB_DEV_PATH;

prop error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn (2)->
    ereport.io.pci.rta@hostbridge/pcibus/pcidev/pcifn,
    ereport.io.pci.target-rta@PCI_HB_DEV_PATH,
    ereport.io.pci.sta@PCI_HB_DEV_PATH;

prop error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.mmu.inval@PCI_HB_DEV_PATH;

prop error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.mmu.prot@PCI_HB_DEV_PATH;

prop error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.mmu.bva@PCI_HB_DEV_PATH;

prop error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

fru cpu;

event fault.io.datapath@cpu, FITrate=CPU_FIT, FRU=cpu;

event error.io.tom.jbus.ibe@hostbridge/pcibus/pcidev/pcifn;

event ereport.io.tom.jbus.ibe@hostbridge{within(5s)};
event ereport.io.tom.jbus.srd@hostbridge{within(5s)};
event ereport.io.tom.jbus.bc@hostbridge{within(5s)};

/*
 * A faulty CPU may cause:
 *
 *  - to:	jbus timeout error.
 *  - bus:	jbus bus error.
 *  - ibe:	illegal byte enable error.
 *  - iis:	illegal coherency install state error.
 *  - um:	jbus unmapped error.
 *  - srd:	foreign RD hitting cache line in S, O or M.
 *  - bc:	bad jbus command.
 */
prop fault.io.datapath@cpu (0)->
    error.io.tom.jbus.to@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.jbus.bus@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.jbus.ibe@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.jbus.iis@hostbridge,
    error.io.tom.jbus.um@hostbridge,
    ereport.io.tom.jbus.srd@hostbridge,
    ereport.io.tom.jbus.bc@hostbridge;

prop error.io.tom.jbus.ibe@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.jbus.ibe@hostbridge;

prop error.io.tom.jbus.ibe@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pci.txta@hostbridge/pcibus/pcidev/pcifn;

event fault.io.hbus@hostbridge,
    FITrate=HBUS_FIT, FRU=hostbridge, ASRU=hostbridge;

event ereport.io.tom.jbus.ape@hostbridge{within(5s)};
event ereport.io.tom.jbus.pwpe@hostbridge{within(5s)};
event ereport.io.tom.jbus.drpe@hostbridge{within(5s)};
event ereport.io.tom.jbus.dwpe@hostbridge{within(5s)};
event ereport.io.tom.jbus.cpe@hostbridge{within(5s)};

/* A faulty host bus may cause:
 *
 *  - ape:	jbus address parity error.
 *  - pwpe:	jbus PIO write parity error.
 *  - drpe:	jbus DMA read parity error.
 *  - dwpe:	jbus DMA write parity error.
 *  - cpe:	jbus control parity error.
 */
prop fault.io.hbus@hostbridge(0)->
    ereport.io.tom.jbus.ape@hostbridge,
    ereport.io.tom.jbus.pwpe@hostbridge,
    ereport.io.tom.jbus.drpe@hostbridge,
    ereport.io.tom.jbus.dwpe@hostbridge,
    ereport.io.tom.jbus.cpe@hostbridge;

fru pcibus/pcidev;
asru pcibus/pcidev/pcifn;

event fault.io.pci.device@hostbridge/pcibus/pcidev/pcifn,
    FITrate=PCI_DEV_FIT, FRU=pcibus/pcidev, ASRU=pcibus/pcidev/pcifn;

event error.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn;
event error.io.tom.pbm.tto@hostbridge/pcibus/pcidev/pcifn;
event error.tom.cpu.berr@cpu;
event error.io.pci.rec-ma@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.rec-mdpe@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.rec-ta@hostbridge/pcibus/pcidev/pcifn;
event error.io.pci.serr@hostbridge/pcibus;

event ereport.io.tom.pbm.tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.s-tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.s-rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.s-ma@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.s-rta@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.s-mdpe@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.target-tto@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.tom.pbm.target-rl@hostbridge/pcibus/pcidev/pcifn{within(5s)};
event ereport.io.pci.rserr@hostbridge/pcibus/pcidev/pcifn{within(5s)};

/*
 * A faulty PCI device may cause:
 *
 *  - rl:	it to exceed the limit on retrying a transaction.
 *  - tto:	it to not assert trdy# within the set timeout.
 */
prop fault.io.pci.device@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.pbm.tto@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.pbm.rl@PCI_HB_DEV_PATH,
    ereport.io.tom.pbm.s-rl@PCI_HB_DEV_PATH;

prop error.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.pbm.target-rl@hostbridge/pcibus/pcidev/pcifn;

prop error.io.tom.pbm.rl@hostbridge/pcibus/pcidev/pcifn (0)->
    error.tom.cpu.berr@cpu;

prop error.io.tom.pbm.tto@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.pbm.tto@PCI_HB_DEV_PATH,
    ereport.io.tom.pbm.s-tto@PCI_HB_DEV_PATH;

prop error.io.tom.pbm.tto@hostbridge/pcibus/pcidev/pcifn (1)->
    ereport.io.tom.pbm.target-tto@hostbridge/pcibus/pcidev/pcifn;

/*
 * Need to add the following tomatillo specific propagations to complete the
 * PCI fault tree.
 */
prop error.io.pci.rec-ma@PCI_HB_DEV_PATH (0)->
    ereport.io.tom.pbm.s-ma@PCI_HB_DEV_PATH;

prop error.io.pci.rec-ta@PCI_HB_DEV_PATH (0)->
    ereport.io.tom.pbm.s-rta@PCI_HB_DEV_PATH;

prop error.io.pci.rec-mdpe@PCI_HB_DEV_PATH (0)->
    ereport.io.tom.pbm.s-mdpe@PCI_HB_DEV_PATH;

prop error.io.pci.rec-ta@PCI_HB_DEV_PATH (0)->
    error.tom.cpu.berr@cpu;

prop error.io.pci.rec-mdpe@PCI_HB_DEV_PATH (0)->
    error.tom.cpu.berr@cpu;

prop error.io.pci.rec-ma@PCI_HB_DEV_PATH (0)->
    error.tom.cpu.berr@cpu;

prop error.io.pci.serr@hostbridge/pcibus (1)->
    ereport.io.pci.rserr@PCI_HB_DEV_PATH;

event ereport.cpu.ultraSPARC-IIIi.berr@cpu{within(5s)};

prop error.tom.cpu.berr@cpu (1)->
    ereport.cpu.ultraSPARC-IIIi.berr@cpu;

event defect.io.pci.driver@hostbridge/pcibus/pcidev/pcifn;

prop defect.io.pci.driver@hostbridge/pcibus/pcidev/pcifn (0)->
    error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn;

event defect.io.pci.driver@hostbridge;

prop defect.io.pci.driver@hostbridge (0)->
    error.io.tom.jbus.um@hostbridge,
    error.io.tom.mmu.inval@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.prot@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.bva@hostbridge/pcibus/pcidev/pcifn,
    error.io.tom.mmu.btt@hostbridge/pcibus/pcidev/pcifn;

event ereport.io.tom.jbus.sgr@hostbridge{within(5s)};
event ereport.io.tom.jbus.spci@hostbridge{within(5s)};
event ereport.io.tom.jbus.snp@hostbridge{within(5s)};
event ereport.io.tom.mmu.ue@hostbridge/pcibus/pcidev/pcifn{within(5s)};

event ereport.io.tom.nodiag@hostbridge;

/*
 * Upset used to hide ereports that can not be currently diagnosed.
 */
engine serd.io.tom.nodiag@hostbridge,
    N=1000, T=1hour, method=persistent,
    trip=ereport.io.tom.nodiag@hostbridge;

event upset.io.tom.nodiag@hostbridge,
    engine=serd.io.tom.nodiag@hostbridge;

prop upset.io.tom.nodiag@hostbridge (0)->
    ereport.io.tom.jbus.sgr@hostbridge,
    ereport.io.tom.jbus.spci@hostbridge,
    ereport.io.tom.jbus.snp@hostbridge,
    ereport.io.tom.mmu.ue@PCI_HB_DEV_PATH,
    ereport.io.tom.nodiag@hostbridge;
